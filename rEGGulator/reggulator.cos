rtar 1 1 32236
seta va00 targ

doif va00 ne null
  doif carr ne null
    * Find the inventory (we're not assuming that it does exist; it may not for some reason)
    rtar 1 2 11
    seta va01 targ
    targ va00
    * Check if we're in the inventory
    doif carr eq va01
      targ carr
      * Open the inventory if it's closed
      doif ov00 eq 0 and clac eq 0
        mesg writ targ 0
      endi
      targ va00
      * Flag that it's in the inventory
      setv va02 1
    endi
   endi

  * Make sure we're not in the inventory and are in a metaroom (we may be in a vehicle
  * that is in the closed inventory, but that's getting too unnecessary to handle)
  doif va02 eq 0 and gmap posx posy ne -1
    cmrt 0
  endi
  stop
endi

* Display
new: comp 1 1 32236 "reggulator" 1 0 5010
pat: fixd 1 "blnk" 0 37 15 0 "WhiteOnTransparentChars"
part 1
frmt 0 0 0 0 0 0 2

ptxt "Eggs:"
pat: fixd 2 "blnk" 0 37 29 0 "WhiteOnTransparentChars"
part 2
frmt 0 0 0 0 0 0 2
ptxt "---"

pat: fixd 3 "blnk" 0 20 40 0 "WhiteOnTransparentChars"
part 3
frmt 0 0 0 0 0 0 1
ptxt "Min:"

pat: fixd 4 "blnk" 0 20 52 0 "WhiteOnTransparentChars"
part 4
frmt 0 0 0 0 0 0 1
ptxt "Max:"

setv ov01 5
pat: text 5 "blnk" 0 65 40 0 1001 "WhiteOnTransparentChars"
part 5
frmt 0 0 0 0 0 0 0
ptxt vtos ov01

setv ov02 10
pat: text 6 "blnk" 0 65 52 0 1002 "WhiteOnTransparentChars"
part 6
frmt 0 0 0 0 0 0 0
ptxt vtos ov02

* Button
pat: butt 7 "reggulator" 1 2 48 67 0 [] 1003 1

* Ports
pat: dull 8 "reggulator" 3 10 31 0
prt: onew 0 "Out" "Out" 15 36
pat: dull 9 "reggulator" 4 10 49 0
prt: inew 1 "In" "In" 15 54 1004

attr 195
perm 90
bhvr 0
accg 10
elas 25
fric 100
* Accept mouse down events
imsk 8

doif game "CreatorX" ne 0 and game "CreatorY" ne 0
  mvto 6071 9144
else
  mvto 5555 3563
  velo 10 0
endi

* Minimum
scrp 1 1 32236 1001
  part 5
  setv va01 stoi ptxt
  doif va01 lt 0 or va01 gt ov02
    snde "buzz"
  else
    * Clear last-clicked part
    setv ov00 0
    setv ov01 va01
    ptxt vtos va01
    * Clear focus
    rgam
    targ game "c3_default_focus"
    part game "c3_default_focus_part"
    fcus
  endi
endm

* Maximum
scrp 1 1 32236 1002
  part 6
  setv va01 stoi ptxt
  doif va01 gt 999 or va01 lt ov01
    snde "buzz"
  else
    * Clear last-clicked part
    setv ov00 0
    setv ov02 va01
    ptxt vtos va01
    * Clear focus
    rgam
    targ game "c3_default_focus"
    part game "c3_default_focus_part"
    fcus
  endi
endm

* Button
scrp 1 1 32236 1003
  snde "beep"
  part 7
  doif pose eq 0
    tick 1
    pose 1
  else
    tick 0
    pose 0
    part 2
    ptxt "---"
  endi
endm

* Mouse down
scrp 1 1 32236 76
  * An agent can only run one script at a time, so clicking anywhere in the
  * game, even if not on the agent, will interrupt the timer script while it's
  * doing the "working" button animation. This means that the rEGGulator will
  * appear to be turned off if this happens. To avoid this, force the light back
  * on if the rEGGulator is actually on
  doif tick eq 100
    part 7
    pose 1
  endi

  * Don't do anything else if it wasn't a left click on us
  doif hots ne ownr or _p1_ ne 1
    stop
  endi

  * Keep the focus on the last-clicked part until enter is pressed if a part is clicked...
  doif hotp eq 5 or hotp eq 6
    * ...and it's one of the input parts...
    doif ov00 eq 5 or ov00 eq 6
      * ...and it's not the one clicked last time
      doif hotp ne ov00
        snde "buzz"
        wait 1
        part ov00
        fcus
      endi
    else
      * If an input part was clicked OK then set it as the last-clicked part
      setv ov00 hotp
      * Change text to yellow
      part hotp
      sets va01 "<tint 255 255 0>"
      adds va01 ptxt
      ptxt va01
    endi
  endi
endm

* In port
scrp 1 1 32236 1004
  part 7
  doif _p1_ ge 1
    tick 1
    pose 1
  else
    tick 0
    pose 0
    part 2
    ptxt "---"
  endi
endm

* Force the plane back when dropped to make sure it (probably) stays visible
scrp 1 1 32236 5
  setv va01 5010
  doif carr eq null
    plne va01
  else
    * If dropped into a vehicle, don't end up behind it (e.g. the DS inventory)
    targ carr
    doif plne > va01
      setv va01 plne
      addv va01 10
    endi
    targ ownr
    plne va01
  endi
endm

* Timer
scrp 1 1 32236 9
  * Most of the script is inst to prevent mouse down interrupting it, which
  * fires even if not clicked on the agent itself
  inst
  tick 100
  setv va01 totl 3 4 1
  * Encourage breeding
  doif va01 lt ov01
    enum 4 1 0
      * Also check chem 48 to let pregnant Norns lay their eggs
      doif dead eq 0 and cage ge 3 and chem 48 lt 0.1
        * Decrease libido lowerer
        chem 40 -1
        * Increase arousal potential
        chem 39 1
        * Decrease tiredness
        chem 154 -0.5
        * Decrease sleepiness
        chem 155 -0.5
        * Decrease crowdedness
        chem 157 -0.75
      endi
    next
    setv va00 va01
    subv va00 ov01
    setv va02 1
  * Discourage breeding
  elif va01 ge ov02
    enum 4 1 0
      doif dead eq 0 and cage ge 3
        * Increase libido lowerer
        chem 40 1
        * Decrease arousal potential
        chem 39 -1
        doif spcs eq 1
          * Decrease testosterone
          chem 53 -1
        elif spcs eq 2
          * Decrease oestrogen
          chem 46 -1
        endi
      endi
    next
    setv va00 va01
    subv va00 ov02
    * Only display as a warning if we are actually over the limit
    doif va01 gt ov02
      setv va02 1
    endi
  endi

  doif va02 eq 0
    * Creatures 3 can't tint text
    doif gnam eq "Docking Station"
      sets va03 "<tint 0 255 0>"
      adds va03 vtos va01
    else
      sets va03 vtos va01
    endi
  else
    doif gnam eq "Docking Station"
      sets va03 "<tint 255 0 0>"
      adds va03 vtos va01
    else
      sets va03 "! "
      adds va03 vtos va01
      adds va03 " !"
    endi
  endi

  prt: send 0 va00

  part 2
  ptxt va03

  * Do the "working" animation
  slow
  part 7
  pose 0
  wait 5
  part 7
  pose 1
endm

rscr

enum 1 1 32236
  kill targ
next
scrx 1 1 32236 5
scrx 1 1 32236 9
scrx 1 1 32236 76
scrx 1 1 32236 1001
scrx 1 1 32236 1002
scrx 1 1 32236 1003
scrx 1 1 32236 1004
