inst

new: simp 1 1 32228 "ebutler" 7 0 1000
attr 198
perm 90
bhvr 0
accg 3
elas 10
fric 100
* Get creator positioning
setv va00 game "CreatorX"
setv va01 game "CreatorY"
* If no values then assume this is C3 only
doif va00 eq 0 and va01 eq 0
  * Move it to a safe C3 location (just above the incubator area)
  setv va00 rand 1400 1550
  setv va01 400
  endi
mvsf va00 va01
frat 2
anim [1 2 3 4 255]
* On/off state
setv ov00 1

* Activate 1
scrp 1 1 32228 1
  snde "1bep"
  doif ov00 eq 0
    anim [1 2 3 4 255]
    setv ov00 1
  else
    anim [0]
    setv ov00 0
  endi
endm

* Speech
scrp 1 1 32228 126
  lock
  inst

  * Abort if turned off
  doif ov00 eq 0
    stop
  endi

  * _p1_ is the text being spoken
  * _p2_ is the creature who is speaking

  * Check for key words
  doif sins _p1_ 1 " hungry for starch" ne -1 or sins _p1_ 1 " hungry for protein" ne -1 or sins _p1_ 1 " hungry for fat" ne -1 or sins _p1_ 1 " bored" ne -1
    anim [5 6 255]
    gsub checkneed
    targ ownr
    anim [1 2 3 4 255]
  endi

  subr checkneed
    * Check for starch hunger
    doif sins _p1_ 1 " hungry for starch" ne -1
      * Get creature's position
      targ _p2_
      setv va01 posx
      setv va02 posy
      * Create 3 justanut pods
      reps 3
        new: simp 2 3 17 "justanut pod" 9 18 51
        attr 194
        perm 64
        bhvr 48
        pose 1
        accg 3
        aero 5
        fric 99
        elas 20
        emit 7 .5
        tick 200
        * Move near creature
        setv va03 va01
        setv va04 va02
        subv va03 rand 0 wdth
        subv va04 hght
        doif tmvt va03 va04 eq 1
          mvto va03 va04
          velo rand -5 5 0
          snde "pl_2"
        else
          kill targ
        endi
        wait 2
        inst
      repe
    endi

    * Check for protein hunger
    doif sins _p1_ 1 " hungry for protein" ne -1
      * Get creature's position
      targ _p2_
      setv va01 posx
      setv va02 posy
      * Create 3 lemons
      reps 3
        new: simp 2 8 7 "ds lemon pod" 2 30 355
        attr 194
        bhvr 48
        accg 2
        perm 64
        fric 100
        elas 30
        aero 2
        sets name "eaten" "no"
        emit 6 .5
        tick 1000
        * Move near creature
        setv va03 va01
        setv va04 va02
        subv va03 rand 0 wdth
        subv va04 hght
        doif tmvt va03 va04 eq 1
          mvto va03 va04
          velo rand -5 5 0
          snde "pl_2"
        else
          kill targ
        endi
        wait 2
        inst
      repe
    endi

    * Check for fat hunger
    doif sins _p1_ 1 " hungry for fat" ne -1
      * Get creature's position
      targ _p2_
      setv va01 posx
      setv va02 posy
      * Create 3 carrots
      reps 3
        new: simp 2 11 9 "carr" 0 0 563
        attr 194
        bhvr 48
        perm 64
        accg 1
        elas 20
        fric 99
        emit 8 .5
        tick 100
        doif rand 0 1 eq 1
          sets name "direction" "right"
          pose 6
        else
          sets name "direction" "left"
          pose 2
        endi
        * Move near creature
        setv va03 va01
        setv va04 va02
        subv va03 rand 0 wdth
        subv va04 hght
        doif tmvt va03 va04 eq 1
          mvto va03 va04
          velo rand -5 5 0
          snde "pl_2"
        else
          kill targ
        endi
        wait 2
        inst
      repe
    endi

    * Check for boredom
    doif sins _p1_ 1 " bored" ne -1
      * Abort if there is already a toy near the creature
      setv va01 0
      enum 2 21 0
        doif va01 eq 0
          doif sqrt disq _p2_ le 100
            * Stop is not used so that the subroutine to exits properly and
            * allows the calling code to continue
            setv va01 1
          endi
        endi
      next
      doif va01 eq 0
        * Get creature's position
        targ _p2_
        setv va01 posx
        setv va02 posy
        * Find a toy - try up to 10 times
        setv va98 0
        loop
          addv va98 1
          inst
          rtar 2 21 0
          doif carr ne pntr
            * Check if it can be picked up by creatures and is autonomous
            setv va99 bhvr
            andv va99 32
            doif va99 eq 32 and movs eq 0
              * Calculate position
              subv va01 rand 0 wdth
              subv va02 hght
              doif tmvt va01 va02 eq 1
                * Move near creature
                snde "pl_2"
                mvto va01 va02
                snde "pl_2"
                * Enforce gravity
                accg accg
                setv va98 -1
              endi
            endi
          endi
          slow
        untl va98 eq 10 or va98 eq -1
      endi
    endi
  retn
endm

rscr

enum 1 1 32228 kill targ next scrx 1 1 32228 1 scrx 1 1 32228 9 scrx 1 1 32228 126
