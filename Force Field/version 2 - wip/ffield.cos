* CREATE
inst
new: comp 1 1 32208 "force field 2" 3 0 1000
pat: butt 1 "force field 2" 3 1 11 10 0 [] 1000 0
pat: butt 2 "force field 2" 3 1 11 30 0 [] 1001 0
attr 194
perm 90
bhvr 0
accg 3
elas 0
fric 100
* Get creator positioning
setv va00 game "CreatorX"
setv va01 game "CreatorY"
* If no values then assume this is C3 only
doif va00 eq 0 and va01 eq 0
  * Move it to a safe C3 location (just above the incubator area)
  setv va00 rand 1400 1550
  setv va01 400
  endi
mvsf va00 va01
setv ov00 1
setv ov01 1
setv ov02 4
setv ov03 1

* OVs
* ov01 - type (0=older than 1=younger than 2=all)
* ov02 - stage limit
* ov03 - race (0=all 1=Norn 2=Ettin 3=Grendel 4=Geat)

* PICK UP
scrp 1 1 32208 4
  inst
  * Destroy any config panels
  enum 1 1 32209
    kill targ
  next
  * Turn off
  tick 0
  pose 0
endm

* CONFIG
scrp 1 1 32208 1000
  inst
  * Set initial text
  sets va00 "PREVENT PASSING:\n"
  doif ov03 eq 0
    adds va00 "\nAll creatures"
  endi
  doif ov03 eq 1
    adds va00 "\nNorns"
  endi
  doif ov03 eq 2
    adds va00 "\nEttins"
  endi
  doif ov03 eq 3
    adds va00 "\nGrendels"
  endi
  doif ov03 eq 4
    adds va00 "\nGeats"
  endi
  doif ov01 eq 0
    adds va00 "\nOlder than"
  endi
  doif ov01 eq 1
    adds va00 "\nYounger than"
  endi
  doif ov01 eq 2
    adds va00 "\nAny life stage"
  endi
  doif ov01 ne 2
    adds va00 "\n"
    adds va00 read "creature_history_life_stage" ov02
  endi
  * Destroy any existing config panels
  enum 1 1 32209
    kill targ
  next
  setv va01 posl
  setv va02 post

*position

  new: comp 1 1 32209 "force field" 1 4 8800
  pat: fixd 1 "force field" 4 0 0 0 "WhiteOnTransparentChars"
  pat: butt 2 "force field" 5 1 128 28 0 [] 1000 0
  pat: butt 3 "force field" 5 1 128 38 0 [] 1001 0
  pat: butt 4 "force field" 5 1 128 48 0 [] 1002 0
  attr 4
  bhvr 0
  accg 0
  elas 0
  fric 100
  mvto va01 va02
  part 1
  ptxt va00
  * Set ov00 to link back to this force field
  seta ov00 ownr
endm

* CONFIG
scrp 1 1 32208 1001
  inst
  doif tick eq 0
    * Turn on
    pose 1
    tick 1
  else
    * Turn off
    tick 0
    pose 0
  endi
endm

* TIMER
scrp 1 1 32208 9
  inst
  setv va01 posx
  * Set a bigger range for Grendels (or all)
  doif ov03 eq 3 or ov03 eq 0
    rnge 90
  else
    rnge 70
  endi

  * Look for nearby Norns
  targ ownr
  doif ov03 eq 0 or ov03 eq 1
    esee 4 1 0
      doif targ <> null
        seta va02 targ
        setv va03 cage
        setv va04 1
        targ ownr
        * Older than check
        doif ov01 eq 0
          doif va03 gt ov02
            setv va04 0
          endi
        endi
        * Younger than check
        doif ov01 eq 1
          doif va03 lt ov02
            setv va04 0
          endi
        endi
        * All check
        doif ov01 eq 2
          setv va04 0
        endi
        doif va04 eq 0
          * Cannot pass - push away
          pose 2
          targ va02
          snde "ffbz"
          doif posx gt va01
            * Push right
            velo 10 -10
          else
            * Push left
            velo -10 -10
          endi
          * Remove urge
          urge writ targ -1 1.1 -1 1.1
        endi
      endi
    next
  endi

  * Look for nearby Ettins
  targ ownr
  doif ov03 eq 0 or ov03 eq 2
    esee 4 3 0
      doif targ <> null
        seta va02 targ
        setv va03 cage
        setv va04 1
        targ ownr
        * Older than check
        doif ov01 eq 0
          doif va03 gt ov02
            setv va04 0
          endi
        endi
        * Younger than check
        doif ov01 eq 1
          doif va03 lt ov02
            setv va04 0
          endi
        endi
        * All check
        doif ov01 eq 2
          setv va04 0
        endi
        doif va04 eq 0
          * Cannot pass - push away
          pose 2
          targ va02
          snde "ffbz"
          doif posx gt va01
            * Push right
            velo 10 -10
          else
            * Push left
            velo -10 -10
          endi
          * Remove urge
          urge writ targ -1 1.1 -1 1.1
        endi
      endi
    next
  endi

  * Look for nearby Grendels
  targ ownr
  doif ov03 eq 0 or ov03 eq 3
    esee 4 2 0
      doif targ <> null
        seta va02 targ
        setv va03 cage
        setv va04 1
        targ ownr
        * Older than check
        doif ov01 eq 0
          doif va03 gt ov02
            setv va04 0
          endi
        endi
        * Younger than check
        doif ov01 eq 1
          doif va03 lt ov02
            setv va04 0
          endi
        endi
        * All check
        doif ov01 eq 2
          setv va04 0
        endi
        doif va04 eq 0
          * Cannot pass - push away
          pose 2
          targ va02
          snde "ffbz"
          doif posx gt va01
            * Push right
            velo 10 -10
          else
            * Push left
            velo -10 -10
          endi
          * Remove urge
          urge writ targ -1 1.1 -1 1.1
        endi
      endi
    next
  endi

  * Look for nearby Geats
  targ ownr
  doif ov03 eq 0 or ov03 eq 4
    esee 4 4 0
      doif targ <> null
        seta va02 targ
        setv va03 cage
        setv va04 1
        targ ownr
        * Older than check
        doif ov01 eq 0
          doif va03 gt ov02
            setv va04 0
          endi
        endi
        * Younger than check
        doif ov01 eq 1
          doif va03 lt ov02
            setv va04 0
          endi
        endi
        * All check
        doif ov01 eq 2
          setv va04 0
        endi
        doif va04 eq 0
          * Cannot pass - push away
          pose 2
          targ va02
          snde "ffbz"
          doif posx gt va01
            * Push right
            velo 10 -10
          else
            * Push left
            velo -10 -10
          endi
          * Remove urge
          urge writ targ -1 1.1 -1 1.1
        endi
      endi
    next
  endi

  targ ownr
  wait 5
  pose 1
endm

* CONFIG PANEL SCRIPTS

* ACTIVATE 1
scrp 1 1 32209 1
  kill ownr
endm

* RACE BUTTON
scrp 1 1 32209 1000
  targ ov00
  addv ov03 1
  doif ov03 gt 4
    setv ov03 0
  endi
  call 1003 0 0
endm

* TYPE
scrp 1 1 32209 1001
  targ ov00
  addv ov01 1
  doif ov01 gt 2
    setv ov01 0
  endi
  call 1003 0 0
endm

* STAGE
scrp 1 1 32209 1002
  targ ov00
  addv ov02 1
  doif ov02 gt 6
    setv ov02 0
  endi
  call 1003 0 0
endm

* DISPLAY
scrp 1 1 32209 1003
  snde "1bep"
  inst
  targ ov00
  sets va00 "PREVENT PASSING:\n"
  doif ov03 eq 0
    adds va00 "\nAll creatures"
  endi
  doif ov03 eq 1
    adds va00 "\nNorns"
  endi
  doif ov03 eq 2
    adds va00 "\nEttins"
  endi
  doif ov03 eq 3
    adds va00 "\nGrendels"
  endi
  doif ov03 eq 4
    adds va00 "\nGeats"
  endi
  doif ov01 eq 0
    adds va00 "\nOlder than"
  endi
  doif ov01 eq 1
    adds va00 "\nYounger than"
  endi
  doif ov01 eq 2
    adds va00 "\nAny life stage"
  endi
  doif ov01 ne 2
    adds va00 "\n"
    adds va00 read "creature_history_life_stage" ov02
  endi
  targ ownr
  part 1
  ptxt va00
endm

rscr
  enum 1 1 32208
    kill targ
  next
  scrx 1 1 32208 1 * Legacy
  scrx 1 1 32208 2 * Legacy
  scrx 1 1 32208 4
  scrx 1 1 32208 9
  scrx 1 1 32208 1000
  scrx 1 1 32208 1001
  * Destroy any config panels
  enum 1 1 32209
    kill targ
  next
  scrx 1 1 32209 1
  scrx 1 1 32209 1000
  scrx 1 1 32209 1001
  scrx 1 1 32209 1002
  scrx 1 1 32209 1003
