* CREATE
inst
new: simp 3 8 32201 "roamer" 9 0 1000
attr 194
perm 60
bhvr 11
accg 5
elas 0
fric 90
setv ov01 0
setv ov02 0
setv ov03 0
setv ov04 0
setv ov05 0
setv ov06 0
setv ov07 0
setv ov08 0
* Get creator positioning
setv va00 game "CreatorX"
setv va01 game "CreatorY"
* If no values then assume this is C3 only
doif va00 eq 0 and va01 eq 0
  * Move it to a safe C3 location (just above the incubator area)
  setv va00 rand 1400 1550
  setv va01 400
  endi
mvsf va00 va01
tick 1

* OVs
* ov01 - direction of travel (velocity)
* ov02 - direction tick counter
* ov03 - wall detection tick counter
* ov04 - last X position - used for wall detection
* ov05 - teleport tick counter
* ov06 - cloud creation tick counter
* ov07 - cloud being created
* ov08 - number of clouds created

* ACTIVATE 1
scrp 3 8 32201 1
  inst
  setv va01 rand 1 3
  doif va01 eq 1
    snde "1bep"
  elif va01 eq 2
    snde "2bep"
  else
    snde "3bep"
  endi
  stim writ from 90 1
  fric 90
  setv ov01 0
  anim []
  pose 0
  velo rand -10 10 rand -10 -20
  slow
  wait 10
endm

* ACTIVATE 2
scrp 3 8 32201 2
  inst
  setv va01 rand 1 3
  doif va01 eq 1
    snde "1bep"
  elif va01 eq 2
    snde "2bep"
  else
    snde "3bep"
  endi
  stim writ from 90 1
  fric 90
  setv ov01 0
  anim []
  pose 0
  velo rand -10 10 rand -10 -20
  slow
  wait 10
endm

* HIT
scrp 3 8 32201 3
  inst
  snde "hit_"
  stim writ from 92 1
  fric 90
  setv ov01 0
  anim []
  pose 0
  velo rand -30 30 rand -10 -30
  slow
  wait 10
endm

* PICK UP
scrp 3 8 32201 4
  tick 0
  stim writ from 91 1
  fric 90
  velo 0 0
  anim []
  pose 0
  setv ov01 0
  setv ov02 0
  setv ov03 0
  setv ov04 0
  setv ov05 0
  setv ov06 0
  setv ov07 0
  setv ov08 0
endm

* DROP
scrp 3 8 32201 5
  tick 1
  * Re-apply accelleration after teleport
  accg 5
endm

* TIMER
scrp 3 8 32201 9
  * Increment tick counters
  addv ov02 1
  addv ov03 1
  addv ov05 1
  addv ov06 1

  * Check for nearby bacteria
  inst
  setv va50 0
  rnge 300
  esee 2 32 23
    addv va50 1
  next
  slow

  * Check for nearby creatures
  inst
  setv va51 0
  rnge 200
  esee 4 0 0
    addv va51 1
  next
  slow

*  doif ov06 ge 50 and rand 0 30 eq 0

  * Release bacteria clouds?
  doif ov06 ge 50 and rand 0 15 eq 0 and va50 ne 0 and rtyp grap posx posy ne 8 and rtyp grap posx posy ne 9
    setv ov07 32215
    setv ov08 0
    setv ov06 0
    * Prevent creature clouds being released as well
    setv va51 0
  endi

  * Release creature clouds?
  doif ov06 ge 50 and rand 0 15 eq 0 and va51 ne 0 and rtyp grap posx posy ne 8 and rtyp grap posx posy ne 9
    * Pick random type
    setv ov07 rand 32213 32214
    setv ov08 0
    setv ov06 0
  endi

  * Release a cloud
  doif ov07 ne 0
    doif ov07 eq 32213
      setv va01 0
    elif ov07 eq 32214
      setv va01 15
    else
      setv va01 30
    endi
    setv va02 posl
    setv va03 post
    doif tmvt va02 va03 eq 1
      snde "spry"
      inst
      new: simp 1 1 ov07 "smoke" 15 va01 990
      attr 64
      alph 120 1
      mvsf va02 va03
      * Set random direction, but make sure isn't standing still
      loop
        velo rand -1 1 rand -1 1
      untl velx ne 0 and vely ne 0
      setv ov00 0
      tick 10
      slow
    endi
    targ ownr
    addv ov08 1
    * Stop when 8 created
    doif ov08 ge 8
      setv ov07 0
    endi
  endi

  * Change direction/stop?
  doif ov02 ge 10
    setv va01 rand 0 2
    doif va01 eq 0
      setv ov01 0
      fric 90
      velo 0 0
      anim []
      pose 0
    * No sudden direction change - only change if stopped
    elif va01 eq 1 and ov01 eq 0
      fric 10
      setv ov01 5
      setv ov04 0
      * Trigger to start animation next timer
      setv ov80 1
    elif va01 eq 2 and ov01 eq 0
      fric 10
      setv ov01 -5
      setv ov04 0
      * Trigger to start animation next timer
      setv ov80 1
    endi
    setv ov02 0
  endi

  * Teleport? (not if creating clouds)
  doif ov05 ge 250 and rand 0 30 eq 0 and ov07 eq 0
    lock
    * Make sure autonomous
    doif movs eq 0
      * Call pickup script - this halts & resets
      call 4 0 0
      * Find new co-ordinates
      loop
        setv va01 rand 0 10000
        setv va02 rand 0 10000
        * Check exclusion zone for airlocks
        setv va03 0
        doif va01 ge 3200 and va02 ge 3600 and va01 le 5600 and va02 le 4500
          setv va03 1
        endi
      untl tmvt va01 va02 eq 1 and va03 eq 0
      * Fade out
      snde "pl_1"
      setv va03 0
      loop
        inst
        alph va03 1
        addv va03 8
        slow
      untl va03 gt 256
      * Move to new position
      inst
      alph 256 1
      mvto va01 va02
      slow
      * Fade in
      snde "pl_1"
      setv va03 256
      loop
        inst
        alph va03 1
        subv va03 8
        slow
      untl va03 lt 0
      alph 0 0
      * Call drop script - this re-enables
      call 5 0 0
      setv ov05 0
    endi
    unlk
  endi

  * Collision check if moving
  doif ov01 ne 0 and ov03 ge 5
    setv va01 posx
    setv va02 posx
    subv va01 10
    addv va02 10
    * Stop if still within 20-pixel range of last check
    doif ov04 gt va01 and ov04 lt va02
      fric 90
      velo 0 0
      anim []
      pose 0
      setv ov01 0
    endi
    setv ov04 posx
    setv ov03 0
  endi

  * Move
  doif ov01 ne 0
    inst
    velo ov01 vely
    doif ov80 eq 1 and ov01 gt 0
      anim [1 2 3 4 5 6 7 8 255]
      setv ov80 0
    endi
    doif ov80 eq 1 and ov01 lt 0
      anim [1 8 7 6 5 4 3 2 255]
      setv ov80 0
    endi
    slow
  endi
endm

* CLOUD 1 TIMER - FEEL GOOD
scrp 1 1 32213 9
  * Change various "feel good" chemicals
  inst
  etch 4 0 0
    doif targ ne null
      * Injury
      chem 90 -0.01
      * Stress
      chem 128 -0.01
      * Pain
      chem 148 -0.01
      * Coldness
      chem 152 -0.01
      * Hotness
      chem 153 -0.01
      * Loneliness
      chem 156 -0.01
      * Crowded
      chem 157 -0.01
      * Fear
      chem 158 -0.01
      * Anger
      chem 160 -0.01
      * Comfort
      chem 162 -0.01
    endi
  next
  targ ownr
  slow
  * Change direction?
  doif rand 0 1 eq 0
    * Set random direction, but make sure isn't standing still
    loop
      velo rand -1 1 rand -1 1
    untl velx ne 0 and vely ne 0
  endi
  * Start animation
  doif pose eq 1
    frat 2
    anim [2 3 4 5 6 7 8 9 255]
  endi
  doif pose eq 0
    pose 1
  endi
  * Life timer/kill
  addv ov00 1
  doif pose eq 13
    kill ownr
  endi
  doif pose eq 12
    pose 13
  endi
  doif pose eq 11
    pose 12
  endi
  doif pose eq 10
    pose 11
  endi
  doif ov00 ge 50
    anim []
    pose 10
    tick 1
    setv ov00 0
  endi
endm

* CLOUD 2 TIMER - NASTY CHEMICALS
scrp 1 1 32214 9
  * Reduce nasty chemicals
  inst
  etch 4 0 0
    doif targ ne null
      setv va01 66
      loop
        chem va01 -0.01
        addv va01 1
      untl va01 eq 90
    endi
  next
  targ ownr
  slow
  * Change direction?
  doif rand 0 1 eq 0
    * Set random direction, but make sure isn't standing still
    loop
      velo rand -1 1 rand -1 1
    untl velx ne 0 and vely ne 0
  endi
  * Start animation
  doif pose eq 1
    frat 2
    anim [2 3 4 5 6 7 8 9 255]
  endi
  doif pose eq 0
    pose 1
  endi
  * Life timer/kill
  addv ov00 1
  doif pose eq 13
    kill ownr
  endi
  doif pose eq 12
    pose 13
  endi
  doif pose eq 11
    pose 12
  endi
  doif pose eq 10
    pose 11
  endi
  doif ov00 ge 50
    anim []
    pose 10
    tick 1
    setv ov00 0
  endi
endm

* CLOUD 3 TIMER - BACTERIA
scrp 1 1 32215 9
  * Kill bacteria
  inst
  rnge 60
  esee 2 32 23
    doif targ ne null
      kill targ
    endi
  next
  targ ownr
  slow
  * Change direction?
  doif rand 0 1 eq 0
    * Set random direction, but make sure isn't standing still
    loop
      velo rand -1 1 rand -1 1
    untl velx ne 0 and vely ne 0
  endi
  * Start animation
  doif pose eq 1
    frat 2
    anim [2 3 4 5 6 7 8 9 255]
  endi
  doif pose eq 0
    pose 1
  endi
  * Life timer/kill
  addv ov00 1
  doif pose eq 13
    kill ownr
  endi
  doif pose eq 12
    pose 13
  endi
  doif pose eq 11
    pose 12
  endi
  doif pose eq 10
    pose 11
  endi
  doif ov00 ge 50
    anim []
    pose 10
    tick 1
    setv ov00 0
  endi
endm

rscr
  * Kill roamer
  enum 3 8 32201
    kill targ
  next
  * Kill clouds
  enum 1 1 32213
    kill targ
  next
  enum 1 1 32214
    kill targ
  next
  enum 1 1 32215
    kill targ
  next
  scrx 3 8 32201 1
  scrx 3 8 32201 2
  scrx 3 8 32201 3
  scrx 3 8 32201 4
  scrx 3 8 32201 5
  scrx 3 8 32201 9
  scrx 1 1 32213 9
  scrx 1 1 32214 9
  scrx 1 1 32215 9
