inst

* Abort if a C3 or docked game
doif type game "ds_game_type" ne 2 or game "ds_game_type" eq "docked"
  stop
endi

* Abort if C3 not installed
doif eame "engine_no_auxiliary_bootstrap_1" ne 1
  stop
endi

* Abort if already present
setv va01 0
rtar 1 1 32224
doif ov02 eq 1
  stop
else
  setv ov02 1
endi

new: comp 1 1 131 "infobar" 1 0 1
attr 16
pat: fixd 1 "infobar" 2 22 13 0 "whiteontransparentchars"
part 1
frmt 2 2 0 0 0 0 2
mvto 700 820

pray refr
new: comp 3 3 30 "egglayer" 17 0 0
attr 28
clac 0
pat: dull 1 "egglayer" 1 19 24 5
part 1
pose 4
pat: dull 2 "egglayer" 7 23 89 1
mvto 672 845

new: comp 3 3 31 "egglayer" 10 17 0
attr 16
pat: butt 1 "egglayer" 18 2 112 30 0 [] 1000 0
part 1
anim [1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 255]
pat: butt 2 "egglayer" 20 2 114 54 0 [] 1001 0
part 2
anim [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 255]
pat: butt 3 "egglayer" 22 2 13 83 0 [] 1002 0
pat: butt 4 "egglayer" 24 2 75 53 0 [] 1003 0
part 4
pose 1
pat: butt 5 "egglayer" 26 2 76 33 0 [] 1004 0
sets ov90 pray next "EGGS" ""

doif pray test ov90 > 0
	setv va23 pray file pray agts ov90 "Egg Glyph File" "blnk.s16" 2 1
	doif pray test pray agts ov90 "Egg Glyph File 2" "blnk.s16" <> 0
		setv va23 pray file pray agts ov90 "Egg Glyph File 2" "blnk.s16" 2 1
	endi
	pat: dull 6 pray agts ov90 "Egg Gallery male" "blnk" 0 20 20 0
	part 6
	anms pray agts ov90 "Egg Animation String" "0"
	sets ov91 pray agts ov90 "Genetics File" "n*"
	seta va98 targ
	sets va99 ov90
	rtar 1 1 131
	doif targ ne null
		part 1
		ptxt va99
	endi
	targ va98
else
	pat: dull 6 "blnk" 0 20 20 0
	sets ov91 "n*"
endi

setv ov00 1
mvto 736 845

scrp 3 3 31 1000
	sndc "sc_1"
	lock
	part 1
	anim [1 0 1 0 1 0]
	inst
	pray refr
	doif pray test ov90 eq 0
		sets ov90 pray next "EGGS" ""
	endi
	sets ov90 pray next "EGGS" ov90
	doif ov90 = ""
		stop
	endi
	sets va99 ov90
	rtar 1 1 131
	doif targ ne null
		part 1
		ptxt va99
	endi

	targ ownr
	doif ov00 eq 1
		part 6
		setv va23 pray file pray agts ov90 "Egg Glyph File" "blnk.s16" 2 1
		doif pray file pray agts ov90 "Egg Glyph File" "blnk.s16" 2 1 eq 1
			gall "blnk" 0
		else
			anim [0]
			gall pray agts ov90 "Egg Gallery male" "blnk" 0
			*anms pray agts ov90 "Egg Animation String" "0"
		endi
	elif ov00 eq 2
		part 6
		setv va23 pray file pray agts ov90 "Egg Glyph File 2" "blnk.s16" 2 1
		doif pray file pray agts ov90 "Egg Glyph File 2" "blnk.s16" 2 1 eq 1
			gall "blnk" 0
		else
			part 6
			anim [0]
			gall pray agts ov90 "Egg Gallery female" "blnk" 0
			*anms pray agts ov90 "Egg Animation String" "0"
		endi
	endi
	sets ov91 pray agts ov90 "Genetics File" "n*"
	slow
	over
	part 1
	anim [1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 255]
	part 2
	anim [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 255]
endm

scrp 3 3 31 1003
	sndc "pl_1"
	inst
	part 4
	pose 1
	part 5
	pose 0
	setv ov00 1

	pray refr
	doif pray test ov90 eq 0
		sets ov90 pray next "EGGS" ""
		sets va99 ov90
		rtar 1 1 131
		doif targ ne null
			part 1
			ptxt va99
		endi
		targ ownr
	endi

	part 6
	doif pray test ov90 > 0
		setv va23 pray file pray agts ov90 "Egg Glyph File" "blnk.s16" 2 1
		doif pray file pray agts ov90 "Egg Glyph File" "blnk.s16" 2 1 eq 1
			gall "blnk" 0
			stop
		endi
		anim [0]
		gall pray agts ov90 "Egg Gallery male" "blnk" 0
		*anms pray agts ov90 "Egg Animation String" "0"
	endi
endm

scrp 3 3 31 1004
	sndc "pl_2"
	inst
	part 4
	pose 0
	part 5
	pose 1
	setv ov00 2

	pray refr
	doif pray test ov90 eq 0
		sets ov90 pray next "EGGS" ""
		sets va99 ov90
		rtar 1 1 131
		doif targ ne null
			part 1
			ptxt va99
		endi
		targ ownr
	endi
	part 6
	doif pray test ov90 > 0
		setv va23 pray file pray agts ov90 "Egg Glyph File 2" "blnk.s16" 2 1
		doif pray file pray agts ov90 "Egg Glyph File 2" "blnk.s16" 2 1 eq 1
			gall "blnk" 0
			stop
		endi
		anim [0]
		gall pray agts ov90 "Egg Gallery female" "blnk" 0
		*anms pray agts ov90 "Egg Animation String" "0"
	endi
endm

scrp 3 3 31 1001
	sndc "beep"
	lock
	part 2
	anim [1 0 1 0 1 0]
	setv va00 ov00
	rtar 3 3 30
	doif targ ne null
		mesg wrt+ targ 1000 va00 0 0
	endi
	targ ownr
	over
	part 1
	anim [1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 255]
	part 2
	anim [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 255]
endm

scrp 3 3 30 1000
	lock
	part 1
	anim [4 3 2 1 0]
	sndc "egg1"
	over
	setv va90 posl
	setv va91 post
	addv va90 10
	addv va91 2
	setv va60 rand 0 10
	mulv va60 8
	rtar 3 3 31
	doif targ ne null
		sets va99 ov90
	endi
	targ ownr
	sets ov90 va99
	pray refr
	doif pray test ov90 = 0
		part 1
		sndc "buzz"
		anim [1 2 1 2 3 2 3 4 3 4]
		over
		stop
	endi

	setv va23 pray deps ov90 1
	doif va23 ne 0
		part 1
		sndc "buzz"
		anim [1 2 1 2 3 2 3 4 3 4]
		over
		stop
	endi
	inst
	rtar 3 3 31
	doif targ ne null
		sets va99 ov91
	endi
	new: simp 3 4 1 "eggs" 8 va60 4
	puhl 0 15 45
	puhl 1 15 35
	puhl 2 15 25
	puhl 3 15 10
	puhl 4 15 10
	puhl 5 15 10
	emit 11 0.65
	gene load targ 1 va99
	setv ov01 _p1_
	elas 10
	fric 100
	attr 195
	bhvr 32
	aero 10
	accg 4
	perm 60
	mvto va90 va91
	tick 900

	setv ov61 100
	targ ownr
	part 1
	anim [0 1 2 3 4]
	over
endm

scrp 3 4 1 6
	doif carr eq null
		plne 5000
	endi
endm

rscr
enum 3 3 30
	kill targ
next
enum 3 3 31
	kill targ
next
enum 1 1 131
	kill targ
next
scrx 3 4 1 6
