inst

* Check if present - abort if not
setv va01 0
enum 1 1 32224
  setv va01 1
next
doif va01 eq 0
  stop
endi

* Enumerate norn area agent to get meta-room ID
enum 1 1 32224
  setv va00 ov00
next

* Delete teleporters
enum 3 2 32204
  kill targ
next
scrx 3 2 32204 1
scrx 3 2 32204 2
scrx 3 2 32204 4
scrx 3 2 32204 5
scrx 3 2 32204 1001

* Move creatures & eggs to meso
enum 4 0 0
  doif gmap posx posy eq va00
    * Get vehicle to drop it if carried
    doif movs eq 3
      seta va01 targ
      targ carr
      dpas 4 0 0
      targ va01
    endi
    mvft 500 9200
  endi
next
enum 3 4 0
  doif gmap posx posy eq va00
    * Get vehicle to drop it if carried
    doif movs eq 3
      seta va01 targ
      targ carr
      dpas 3 4 0
      targ va01
    endi
    mvto 500 9200
    accg 4
  endi
next

* Move portable portals to workshop
enum 3 9 1
  doif gmap posx posy eq va00
    mvto 4526 9023
    doif ov03 eq 1 or ov03 eq 2
      * Close portal
      lock
      sndc "por2"
      bhvr 0
      setv ov03 0
      anim [18 17 16 15 14 13 12 11 10 9 8 7 6 5 4 3 2 1 0]
      attr 199
      clac 2
    endi
  endi
next

* Remove specific agents of family 1
* ...Apples
enum 1 1 129
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Hawk
enum 1 1 43
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 1 1 21
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Hover vehicle
enum 1 1 110
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 1 1 43
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Kingfisher
enum 1 1 5
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Light & heat emitters NT
enum 1 1 12
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 1 1 13
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 1 1 14
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Norn atmosphere machine
enum 1 1 23
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 1 1 17
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Norn cutaways 01
enum 1 1 2
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 1 1 3
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 1 1 4
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Norn egg layer
enum 1 1 131
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Norn platform
enum 1 1 98
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Seed launcher
enum 1 1 109
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Water smell emitter
enum 1 1 15
  doif gmap posx posy eq va00
    kill targ
  endi
next
* ...Wetness monitor and designator
enum 1 1 106
  doif gmap posx posy eq va00
    kill targ
  endi
next

* Remove all agents of families 2 and 3
enum 2 0 0
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 3 0 0
  doif gmap posx posy eq va00
    kill targ
  endi
next

* Other agents
enum 1 1 32225
  doif gmap posx posy eq va00
    kill targ
  endi
next

* Bioengergy incrementer - only remove if there are no other C3-in-DS rooms
rtar 1 1 105
subv ov01 1
doif ov01 eq 0
  kill targ
endi
rtar 1 1 32224
kill targ

* Set LINKs to 0 otherwise the game will crash hugely
setv va66 grap 1400 500
setv va67 grap 1400 760
link va66 va67 0
setv va66 grap 1400 930
link va66 va67 0
setv va66 grap 2818 256
setv va67 grap 2818 475
link va66 va67 0
setv va66 grap 2818 760
link va66 va67 0
* Internal door
link grap 1208 755 grap 1235 755 0

* Remove rooms
sets va01 erid va00
* Find second space (starts with a space)
setv va02 sins va01 2 " "

loop
  * Get everthing up to this space (not including first space or this one)
  subv va02 1
  sets va03 subs va01 2 va02
  delr stoi va03
  * Remove the first bit of the string
  setv va04 strl va01
  subv va04 va02
  addv va02 1
  sets va03 subs va01 va02 va04
  sets va01 va03
  * Find second space (starts with a space)
  setv va02 sins va01 2 " "
untl va02 eq -1

* Delete final room
delr stoi va01

* Delete meta-room
delm va00

* Kill room agent
enum 1 1 32224
  stpt
  kill targ
next
scrx 1 1 32224 9

* Delete room favourite icon
enum 1 3 1
  kill targ
next
enum 1 4 1
  setv va50 ov50
  kill targ
next
enum 1 4 0
  doif ov50 > va50
    subv ov50 1
    setv va02 ov50
    mulv va02 50
    addv va02 145
    flto va02 0
  endi
next
subv game "ds_favourites" 1

* Re-create splash screen room
brmi 0 0
setv va00 addm 0 0 800 600 "DS_splash"
mmsc 100 100 "ds_music.mng\\StringsSolo"
