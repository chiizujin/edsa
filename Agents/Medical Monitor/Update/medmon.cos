* Original agent by Emmental, edits by SunlitMiracle. Feel free to edit and reupload as desired. Last updated December 29th 2024.

* Update 1.4.1 (retroactively named)
* Truncation of creature names longer than 16 characters; renamed (D) marker and made it its own trackable condition; non-Norns tracked by default
* Added tracking for: Pregnancy, Glycogen, Oxygen, Water, Air, ATP, Wounded, Hunger for Fat/Carbs/Protein, Coldness, Hotness
* Tracking of custom chemicals 110 (Body Heat), 130 (Tryptamine), Oxygenated Hemoglobin (221). Body Heat and Oxygenated Hemoglobin should ideally only trigger for True Warmbloods, but false positives and negatives are possible with mutations and certain custom breeds.
* Some chemicals display best with the Unoffical Chemical Names and Short Chem Names catalogues.

* Update 1.4.2
* Adjustments to various detection thresholds to better reflect creatures' needs.
* Updated the in-game help file with new information, also added tooltips.

* CREATE
inst
* Check if already installed
setv va00 0
enum 1 1 32204
	setv va00 1
next
doif va00 eq 1
	stop
endi
* Create it
new: comp 1 1 32204 "medical monitor" 11 0 8900
*attr 52
attr 308
clac 0
bhvr 0
accg 0
elas 0
fric 100
imsk 3
pat: fixd 1 "medical monitor" 1 0 0 0 "WhiteOnTransparentChars"
pat: fixd 2 "medical monitor" 1 130 0 0 "WhiteOnTransparentChars"
*pat: fixd 3 "medical monitor" 1 0 170 0 "WhiteOnTransparentChars"
pat: butt 4 "medical monitor" 2 2 302 132 0 [] 1001 1
pat: butt 5 "medical monitor" 4 2 302 160 0 [] 1002 1
pat: butt 6 "medical monitor" 6 2 300 10 0 [] 1003 1
* Species buttons
pat: butt 8 "medical monitor 2" 0 2 299 48 0 [] 1004 1
pat: butt 9 "medical monitor 2" 2 2 299 68 0 [] 1005 1
pat: butt 10 "medical monitor 2" 4 2 299 88 0 [] 1006 1
pat: butt 11 "medical monitor 2" 6 2 299 108 0 [] 1007 1
* Button just used for status icon
pat: butt 7 "medical monitor" 8 3 344 95 0 [] 1000 0
* Display page
setv ov01 0
* Warning
setv ov02 0
* CTRL state
setv ov04 0
pose 0
* Button images
part 4
pose 1
part 5
pose 1
part 8
pose 1
part 9
pose 1
part 10
pose 1
part 11
pose 1
* Initial text
part 1
ptxt "(Waiting)"
part 0
setv ov05 1
setv ov06 1
setv ov07 1
setv ov08 1
* Check if norn stats installed (can't seem to use OWNR here)
seta va00 targ
setv ov03 401
enum 1 1 32207
* Check if other agent at upper position
	doif ov03 ne 201
		targ va00
		setv ov03 201
	endi
next
targ va00
flto 0 ov03
tick 1

* OVs
* ov01 - display page
* ov02 - warning
* ov03 - y position (nornstats installed?)
* ov04 - CTRL state
* ov05 - Norns
* ov06 - Ettins
* ov07 - Grendels
* ov08 - Geats

* ACTIVATE 1
scrp 1 1 32204 1
	lock
	enum 1 2 14
		seta va00 targ
	next
	targ va00
	snde "gui3"
	targ ownr
	setv va00 0
	loop
		subv va00 10
		flto va00 ov03
	untl va00 lt -320
	flto -334 ov03
	clac 1
endm

* ACTIVATE 2
scrp 1 1 32204 2
	lock
* Play sound from top left GUI agent as agent may be off screen so sound would not be heard
	enum 1 2 14
		seta va00 targ
	next
	targ va00
	snde "gui1"
	targ ownr
	setv va00 -334
	loop
		addv va00 10
		flto va00 ov03
	untl va00 gt -10
	flto 0 ov03
	clac 0
endm

* TIMER
scrp 1 1 32204 9
	doif tick eq 1
		tick 80
	endi
* Loop through Norns
	sets va01 ""
	sets va02 ""
	sets va97 ""
	inst

* Loop through Norns
	targ ownr
	doif ov05 eq 1
		setv va66 1
		gsub checkmed
	endi

* Loop through Ettins
	targ ownr
	doif ov06 eq 1
		setv va66 3
		gsub checkmed
	endi

* Loop through Grendels
	targ ownr
	doif ov07 eq 1
		setv va66 2
		gsub checkmed
	endi

* Loop through Geats
	targ ownr
	doif ov08 eq 1
		setv va66 4
		gsub checkmed
	endi

* Display
	doif va01 eq ""
		sets va01 "(All clear)"
		setv ov02 0
		part 7
		pose 0
	else
* Remove last to prevent page wrap if exactly fills a page
		setv va04 strl va01
		subv va04 1
		sets va01 subs va01 1 va04
		setv va04 strl va02
		subv va04 1
		sets va02 subs va02 1 va04
		doif ov02 eq 0
* Play sound from top left GUI agent as Medical monitor may be off screen so sound would not be heard
			enum 1 2 14
				seta va00 targ
			next
			targ va00
			snde "htal"
			targ ownr
			setv ov02 1
		endi
		part 7
		pose 1
	endi
	part 1
	ptxt va01
* Check page number
	setv va05 npgs
	doif ov01 ge npgs
		setv ov01 npgs
		subv ov01 1
	endi
	page ov01
	part 2
	ptxt va02
	page ov01
* Button images
	part 4
	pose 1
	doif ov01 ne 0
		pose 0
	endi
	part 5
	pose 1
	subv va05 1
	doif ov01 lt va05
		pose 0
	endi
	part 0
	slow

	subr checkmed
		enum 4 va66 0
			doif targ <> null
* Get creature's agent & name
				seta va99 targ
				sets va95 gtos 0
				sets va98 hist name gtos 0
				doif strl va98 gt 16
					sets va98 subs va98 1 16
					adds va98 "..."
				endi
				doif va98 eq ""
					sets va98 "(Unnamed)"
				endi
* Old code for adding death marker to name
*        doif dead eq 1
*          sets va96 " (D)"
*        else
				sets va96 ""
* Check if creature is dead
				doif dead eq 1
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 "Dead"
					adds va02 "\n"
				endi
* Check if creature is pregnant
				doif loci 1 1 2 1 eq 1
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 "Pregnant"
					adds va02 "\n"
				endi
* Check for any bacteria
				setv va03 0
				enum 2 32 23
					doif targ <> null
						doif ov00 = va99
							addv va03 1
						endi
					endi
				next
				targ va99
* Add to list if any found
				doif va03 gt 0
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 vtos va03
					adds va02 " Bacteria\n"
				endi
* Check for Glycogen (4) equal to or less than 5. Might be a bit high for the threshold, but in my experience glycogen tends to bottom out pretty fast at this point anyway.

				doif chem 4 le 0.05
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "chemical_names" 4
					adds va02 " ("
					setv va03 chem 4
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
* Check for oxygen (30) equal to or less than than 40
				doif chem 30 le 0.4
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "chemical_names" 30
					adds va02 " ("
					setv va03 chem 30
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
* Check for water (33) equal to or less than 30
				doif chem 33 le 0.3
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "chemical_names" 33
					adds va02 " ("
					setv va03 chem 33
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
* Check for low ATP and Energy equal to or less than 35. This gives occasional false positives, but going much lower means a creature is usually seconds from death by the time the player is notified. A better warning sign is monitoring other vitals before it gets to this point.
				setv va00 34
				loop
					doif chem va00 le 0.35
						doif va97 ne va95
							doif va01 ne ""
								adds va01 "\n"
								adds va02 "\n"
							endi
							adds va01 va98
							adds va01 va96
							sets va97 va95
						endi
						adds va01 "\n"
						adds va02 read "chemical_names" va00
						adds va02 " ("
						setv va03 chem va00
						mulv va03 100.0
						setv va03 ftoi va03
						adds va02 vtos va03
						adds va02 ")\n"
					endi
					addv va00 1
				untl va00 gt 35
* Big overhaul of the toxin check. Commenting out the code I had in the previous release:
*				setv va00 66
*				loop
*					doif chem va00 ge 0.01
*						doif va97 ne va95
*							doif va01 ne ""
*								adds va01 ""
*								adds va02 ""
*							endi
*							adds va01 va98
*							adds va01 va96
*							sets va97 va95
*						endi
*						adds va01 ""
*						adds va02 read "chemical_names" va00
*						adds va02 " ("
*						setv va03 chem va00
*						mulv va03 100.0
*						setv va03 ftoi va03
*						adds va02 vtos va03
*						adds va02 ")"
*					endi
*					addv va00 1
*				untl va00 gt 90
* Check for heavy metals, cyanide, and belladonna (66-68) equal to or greater than 1. The first usually doesn't decay on its own and the other two can kill in seconds, so they're considered very dangerous.
				setv va00 66
				loop
					doif chem va00 ge 0.01
						doif va97 ne va95
							doif va01 ne ""
								adds va01 "\n"
								adds va02 "\n"
							endi
							adds va01 va98
							adds va01 va96
							sets va97 va95
						endi
						adds va01 "\n"
						adds va02 read "chemical_names" va00
						adds va02 " ("
						setv va03 chem va00
						mulv va03 100.0
						setv va03 ftoi va03
						adds va02 vtos va03
						adds va02 ")\n"
					endi
					addv va00 1
				untl va00 gt 68
* Check toxins 69-75 equal to or greater than 10. Might be a might too sensitive, especially on genomes with robust immune systems.
				setv va00 69
				loop
					doif chem va00 ge 0.1
						doif va97 ne va95
							doif va01 ne ""
								adds va01 "\n"
								adds va02 "\n"
							endi
							adds va01 va98
							adds va01 va96
							sets va97 va95
						endi
						adds va01 "\n"
						adds va02 read "chemical_names" va00
						adds va02 " ("
						setv va03 chem va00
						mulv va03 100.0
						setv va03 ftoi va03
						adds va02 vtos va03
						adds va02 ")\n"
					endi
					addv va00 1
				untl va00 gt 75
* Check ATP Decoupler (78) equal to or greater than 1. It's not likely you'll get this small a dose accidentally and going much higher will kill most creatures, but for the slim chance this will save a creature we'll try warning for it anyway.
				doif chem 78 ge 0.01
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "chemical_names" 78
					adds va02 " ("
					setv va03 chem 78
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
* Check toxins 79-81 equal to or greater than 10.
				setv va00 79
				loop
					doif chem va00 ge 0.1
						doif va97 ne va95
							doif va01 ne ""
								adds va01 "\n"
								adds va02 "\n"
							endi
							adds va01 va98
							adds va01 va96
							sets va97 va95
						endi
						adds va01 "\n"
						adds va02 read "chemical_names" va00
						adds va02 " ("
						setv va03 chem va00
						mulv va03 100.0
						setv va03 ftoi va03
						adds va02 vtos va03
						adds va02 ")\n"
					endi
					addv va00 1
				untl va00 gt 81
* Check Antigens (82-89) equal to or greater than 20. Some genomes use trace amounts for immunity-building so I don't wanna penalize that.
				setv va00 82
				loop
					doif chem va00 ge 0.2
						doif va97 ne va95
							doif va01 ne ""
								adds va01 "\n"
								adds va02 "\n"
							endi
							adds va01 va98
							adds va01 va96
							sets va97 va95
						endi
						adds va01 "\n"
						adds va02 read "chemical_names" va00
						adds va02 " ("
						setv va03 chem va00
						mulv va03 100.0
						setv va03 ftoi va03
						adds va02 vtos va03
						adds va02 ")\n"
					endi
					addv va00 1
				untl va00 gt 89
* Check Wounded (90) equal to or greater than 10. Even though too much of this will directly cause death, there's usually enough time to react and heal the creature.
				doif chem 90 ge 0.1
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "chemical_names" 90
					adds va02 " ("
					setv va03 chem 90
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
* Check for body heat (110) either greater than or equal to 60 or less than or equal to 35. Ideally should only trigger for TWBs by detecting trace amounts of Body Heat, but fringe mutations might cause false positives and false negatives. Conflicts with Grendels of Minimordor. Unofficial Catalogue Names recommended.
				doif chem 110 ge 0.6 or chem 110 le 0.35 and chem 110 gt 0
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "chemical_names" 110
					adds va02 " ("
					setv va03 chem 110
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
* Check for tryptamine (130) greater than or equal to 20. Only has an effect on CFFs and genomes that built off of them, but since there's not an easy way to detect that, this will appear for everyone. Unofficial Catalogue Names recommended.
				doif chem 130 ge 0.2
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "chemical_names" 130
					adds va02 " ("
					setv va03 chem 130
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
* Check for pain (148) greater than or equal to 10. Vanilla breeds tend to flinch or limp starting around this point.
				doif chem 148 ge 0.1
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "chemical_names" 148
					adds va02 " ("
					setv va03 chem 148
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
* Check for hunger (149-151) greater than or equal to 80. Short Catalogue Names recommended.
				setv va00 149
				loop
					doif chem va00 ge 0.80
						doif va97 ne va95
							doif va01 ne ""
								adds va01 "\n"
								adds va02 "\n"
							endi
							adds va01 va98
							adds va01 va96
							sets va97 va95
						endi
						adds va01 "\n"
						adds va02 read "short_chemical_names" va00
						adds va02 " ("
						setv va03 chem va00
						mulv va03 100.0
						setv va03 ftoi va03
						adds va02 vtos va03
						adds va02 ")\n"
					endi
					addv va00 1
				untl va00 gt 151
* Check for Hotness and Coldness (152-153) greater than or equal to 25.
				setv va00 152
				loop
					doif chem va00 ge 0.25
						doif va97 ne va95
							doif va01 ne ""
								adds va01 "\n"
								adds va02 "\n"
							endi
							adds va01 va98
							adds va01 va96
							sets va97 va95
						endi
						adds va01 "\n"
						adds va02 read "chemical_names" va00
						adds va02 " ("
						setv va03 chem va00
						mulv va03 100.0
						setv va03 ftoi va03
						adds va02 vtos va03
						adds va02 ")\n"
					endi
					addv va00 1
				untl va00 gt 153
* Check for oxygenated blood less than or equal to 20. Like with Body Heat, this should only trigger for TWBs unless there's a weird mutation. Recommended to use Verm's shortened chemical names.
				doif chem 221 le 0.2 and chem 110 gt 0
					doif va97 ne va95
						doif va01 ne ""
							adds va01 "\n"
							adds va02 "\n"
						endi
						adds va01 va98
						adds va01 va96
						sets va97 va95
					endi
					adds va01 "\n"
					adds va02 read "short_chemical_names" 221
					adds va02 " ("
					setv va03 chem 221
					mulv va03 100.0
					setv va03 ftoi va03
					adds va02 vtos va03
					adds va02 ")\n"
				endi
				addv va00 1
			endi
		next
	retn
endm

* CLICKING ON ICON
scrp 1 1 32204 1000
	doif clac eq 0
		call 1 0 0
	else
		call 2 0 0
	endi
endm

* UP BUTTON
scrp 1 1 32204 1001
	snde "1bep"
	inst
	subv ov01 1
	part 1
	setv va05 npgs
	doif ov01 lt 0
		setv ov01 0
	endi
	page ov01
	part 2
	page ov01
* Button images
	part 4
	pose 1
	doif ov01 ne 0
		pose 0
	endi
	part 5
	pose 1
	subv va05 1
	doif ov01 lt va05
		pose 0
	endi
	part 0
	slow
endm

* DOWN BUTTON
scrp 1 1 32204 1002
	snde "1bep"
	inst
	addv ov01 1
	part 1
	setv va05 npgs
	doif ov01 ge npgs
		setv ov01 npgs
		subv ov01 1
	endi
	page ov01
	part 2
	page ov01
* Button images
	part 4
	pose 1
	doif ov01 ne 0
		pose 0
	endi
	part 5
	pose 1
	subv va05 1
	doif ov01 lt va05
		pose 0
	endi
	part 0
	slow
endm

* ON/OFF BUTTON
scrp 1 1 32204 1003
	snde "1bep"
	inst
	doif tick eq 0
		tick 1
		part 6
		pose 0
		part 1
		ptxt "(Waiting)"
		setv ov02 0
		part 7
		pose 0
	else
		tick 0
		part 6
		pose 1
		part 1
		ptxt "(Disabled)"
		part 7
		pose 2
	endi
	part 2
	ptxt ""
	part 4
	pose 1
	part 5
	pose 1
	part 0
	slow
endm

* NORN BUTTON
scrp 1 1 32204 1004
	snde "1bep"
	inst
	doif ov05 eq 0
		setv ov05 1
		part 8
		pose 1
	else
		setv ov05 0
		part 8
		pose 0
	endi
	slow
	doif tick ne 0
		call 9 0 0
	endi
endm

* ETTIN BUTTON
scrp 1 1 32204 1005
	snde "1bep"
	inst
	doif ov06 eq 0
		setv ov06 1
		part 9
		pose 1
	else
		setv ov06 0
		part 9
		pose 0
	endi
	slow
	doif tick ne 0
		call 9 0 0
	endi
endm

* GRENDEL BUTTON
scrp 1 1 32204 1006
	snde "1bep"
	inst
	doif ov07 eq 0
		setv ov07 1
		part 10
		pose 1
	else
		setv ov07 0
		part 10
		pose 0
	endi
	slow
	doif tick ne 0
		call 9 0 0
	endi
endm

* GEAT BUTTON
scrp 1 1 32204 1007
	snde "1bep"
	inst
	doif ov08 eq 0
		setv ov08 1
		part 11
		pose 1
	else
		setv ov08 0
		part 11
		pose 0
	endi
	slow
	doif tick ne 0
		call 9 0 0
	endi
endm

* RAW KEY DOWN
scrp 1 1 32204 73
* Check if 4 pressed while CTRL down
	doif ov04 eq 1 and _p1_ eq 52
		doif clac eq 0
* Slide out
			lock
			enum 1 2 14
				seta va00 targ
			next
			targ va00
			snde "gui3"
			targ ownr
			setv va00 0
			loop
				subv va00 10
				flto va00 ov03
			untl va00 lt -320
			flto -334 ov03
			clac 1
		else
* Slide in
			lock
* Play sound from top left GUI agent as Medical monitor may be off screen so sound would not be heard
			enum 1 2 14
				seta va00 targ
			next
			targ va00
			snde "gui1"
			targ ownr
			setv va00 -334
			loop
				addv va00 10
				flto va00 ov03
			untl va00 gt -10
			flto 0 ov03
			clac 0
		endi
	endi
* Flag if CTRL pressed
	doif _p1_ eq 17
		setv ov04 1
	endi
endm

* RAW KEY UP
scrp 1 1 32204 74
* Clear flag if CTRL released
	doif _p1_ eq 17
		setv ov04 0
	endi
endm

* DESTROY
rscr
enum 1 1 32204
	kill targ
next
scrx 1 1 32204 1
scrx 1 1 32204 2
scrx 1 1 32204 9
scrx 1 1 32204 1001
scrx 1 1 32204 1002
scrx 1 1 32204 1003