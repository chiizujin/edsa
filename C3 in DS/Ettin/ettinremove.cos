inst

rtar 1 1 32217
doif targ eq null
  stop
endi

* Get meta-room ID
setv va00 ov00

* Delete teleporters
enum 3 2 32201
  kill targ
next
scrx 3 2 32201 1
scrx 3 2 32201 2
scrx 3 2 32201 4
scrx 3 2 32201 5
scrx 3 2 32201 1001

* Move creatures & eggs to meso
enum 4 0 0
  doif gmap posx posy eq va00
    * Get vehicle to drop it if carried
    doif movs eq 3
      seta va01 targ
      targ carr
      dpas 4 0 0
      targ va01
    endi
    mvft 500 9200
  endi
next
enum 3 4 0
  doif gmap posx posy eq va00
    * Get vehicle to drop it if carried
    doif movs eq 3
      seta va01 targ
      targ carr
      dpas 3 4 0
      targ va01
    endi
    mvto 500 9200
    accg 4
  endi
next

* Move portable portals to workshop
enum 3 9 1
  doif gmap posx posy eq va00
    mvto 4526 9023
    doif ov03 eq 1 or ov03 eq 2
      * Close portal
      lock
      sndc "por2"
**      stop timer
*      tick 0
      bhvr 0
      setv ov03 0
      anim [18 17 16 15 14 13 12 11 10 9 8 7 6 5 4 3 2 1 0]
*      over
      attr 199
      clac 2
    endi
  endi
next

* Remove all agents
enum 1 0 0
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 2 0 0
  doif gmap posx posy eq va00
    kill targ
  endi
next
enum 3 0 0
  doif gmap posx posy eq va00
    kill targ
  endi
next

* Stop any troublesome scripts and remove agents
* ...Rain maker
enum 1 1 113
  stpt
  kill targ
next
* ...Egg layer
enum 1 1 101
  stpt
  kill targ
next
* ...Meerk
enum 2 15 23
  stpt
  kill targ
next

* Set LINKs to 0 otherwise the game will crash hugely
setv va66 grap 6353 259
setv va67 grap 6353 460
link va66 va67 0
setv va66 grap 6353 728
link va66 va67 0
setv va66 grap 5280 751
setv va67 grap 5280 966
link va66 va67 0

* Remove rooms
sets va01 erid va00
* Find second space (starts with a space)
setv va02 sins va01 2 " "

loop
  * Get everthing up to this space (not including first space or this one)
  subv va02 1
  sets va03 subs va01 2 va02
  delr stoi va03
  * Remove the first bit of the string
  setv va04 strl va01
  subv va04 va02
  addv va02 1
  sets va03 subs va01 va02 va04
  sets va01 va03
  * Find second space (starts with a space)
  setv va02 sins va01 2 " "
untl va02 eq -1

* Delete final room
delr stoi va01

* Delete meta-room
delm va00

* Kill room agent
enum 1 1 32217
  stpt
  kill targ
next
scrx 1 1 32217 9

* Delete room favourite icon
enum 1 3 3
  kill targ
next
enum 1 4 3
  setv va50 ov50
  kill targ
next
enum 1 4 0
  doif ov50 > va50
    subv ov50 1
    setv va02 ov50
    mulv va02 50
    addv va02 145
    flto va02 0
  endi
next
subv game "ds_favourites" 1
