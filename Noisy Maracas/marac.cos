* CREATE
inst
new: simp 2 21 32201 "noisy maracas" 6 0 2000
attr 194
perm 90
bhvr 43
accg 3
elas 10
fric 100
puhl 0 30 10
puhl 1 30 10
puhl 2 30 10
puhl 3 10 10
puhl 4 10 10
puhl 5 10 10
* Get creator positioning
setv va00 game "CreatorX"
setv va01 game "CreatorY"
* If no values then assume this is C3 only
doif va00 eq 0 and va01 eq 0
  * Move it to a safe C3 location (just above the incubator area)
  setv va00 rand 1400 1550
  setv va01 400
  endi
mvsf va00 va01
setv ov02 0
tick 1

* OVs
* ov01 - drop pose
* ov02 - agent in use (to prevent timer changing pose)

* ACTIVATE 1
scrp 2 21 32201 1
  lock
  * Make the creature pick it up
  doif carr eq null
    urge writ from 21 1 6 1
    * Give it the stim
    stim writ from 89 1
    stop
  endi
  setv ov02 1
  * Slightly irritate nearby creatures
  inst
  seta va00 from
  rnge 500
  esee 4 0 0
    doif targ ne null
      doif targ ne va00
        setv va01 rand 1 10
        divv va01 100.0
        chem 160 va01
        endi
      endi
  next
  slow
  frat 2
  doif ov01 eq 3
    anim [5 4 5 4 5 4]
  else
    anim [2 1 2 1 2 1]
  endi
  snde "mrc1"
  stim writ from 89 1
  wait 12
  setv ov02 0
  unlk
endm

* ACTIVATE 2
scrp 2 21 32201 2
  lock
  * Make the creature pick it up
  doif carr eq null
    urge writ from 21 1 6 1
    * Give it the stim
    stim writ from 89 1
    stop
  endi
  setv ov02 1
  * Slightly irritate nearby creatures
  inst
  seta va00 from
  rnge 500
  esee 4 0 0
    doif targ ne null
      doif targ ne va00
        setv va01 rand 1 10
        divv va01 100.0
        chem 160 va01
        endi
      endi
  next
  slow
  frat 2
  doif ov01 eq 3
    anim [5 4 5 4 5 4]
  else
    anim [2 1 2 1 2 1]
  endi
  snde "mrc1"
  stim writ from 89 1
  wait 12
  setv ov02 0
  unlk
endm

* HIT
scrp 2 21 32201 3
  * Slightly irritate nearby creatures
  inst
  rnge 100
  esee 4 0 0
    doif targ ne null
      setv va01 rand 1 10
      divv va01 100.0
      chem 160 va01
      endi
  next
  slow
  targ ownr
  snde "mrc2"
  stim writ from 92 1
  * Fly through air
  velo rand -15 15 rand -5 -15
endm

* PICK UP
scrp 2 21 32201 4
  lock
  doif from ne pntr
    * Check direction
    targ from
    doif dirn eq 2
      targ ownr
      pose 4
      setv ov01 3
    elif dirn eq 3
      targ ownr
      pose 1
      setv ov01 0
    endi
    targ ownr
    * Make the creature push it
    urge writ from 21 1 1 1
    setv ov02 1
    * Slightly irritate nearby creatures
    inst
    seta va00 from
    rnge 500
    esee 4 0 0
      doif targ ne null
        doif targ ne va00
          setv va01 rand 1 10
          divv va01 100.0
          chem 160 va01
          endi
        endi
    next
    slow
    frat 2
    doif ov01 eq 3
      anim [5 4 5 4 5 4]
    else
      anim [2 1 2 1 2 1]
    endi
    snde "mrc1"
    stim writ from 89 1
    wait 12
    setv ov02 0
    unlk
  endi
endm

* COLLISION
scrp 2 21 32201 6
  snde "mrc2"
  * Slightly irritate nearby creatures
  inst
  rnge 100
  esee 4 0 0
    doif targ ne null
      setv va01 rand 1 10
      divv va01 100.0
      chem 160 va01
      endi
  next
endm

* TIMER
scrp 2 21 32201 9
  inst
  * Check if in use
  setv va01 0
  doif carr ne null
    targ carr
    setv va01 fmly
  endi
  doif ov02 eq 0 and va01 eq 4
    * Check if creature holding
    doif fmly eq 4
      * Check direction
      doif dirn eq 2
        targ ownr
        pose 4
        setv ov01 3
      elif dirn eq 3
        targ ownr
        pose 1
        setv ov01 0
      endi
    else
      * Not held by creature
      targ ownr
      pose ov01
      setv ov02 0
    endi
    slow
  else
    * Not held
    targ ownr
    pose ov01
    setv ov02 0
  endi
endm

* DESTROY
rscr
  enum 2 21 32201
    kill targ
  next
  scrx 2 21 32201 1
  scrx 2 21 32201 2
  scrx 2 21 32201 3
  scrx 2 21 32201 4
  scrx 2 21 32201 6
  scrx 2 21 32201 9
