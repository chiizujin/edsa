inst

* Abort if Ainarda not present
doif gmap 92000 4500 eq -1
  stop
endi

* Abort if Creatures 3 not installed
doif reaq "patch_level" = 0
  stop
endi

reps 2
  new: simp 2 15 60708 "man-o-war" 87 0 4100
repe

scrp 2 15 60708 10
  attr 192
  accg 0
  aero 1
  perm 100

  frat 2
  doif rand 1 2 eq 1
    anim [0 1 2 3 4 5 6 7 8 9 10 255]
    velo rand -1 -3 rand -2 2
    setv ov01 1
  else
    anim [11 12 13 14 15 16 17 18 19 20 21 255]
    velo rand 1 2 rand -1 1
    setv ov01 2
  endi

  tick 10
  mvto 94837 6380
endm

scrp 2 15 60708 9
  inst

  * Get Ainarda metaroom
  rtar 2 15 60700
  setv va00 ov00
  targ ownr

  * Check if we're out of water
  doif rtyp grap posx post ne 8 and rtyp grap posx post ne 9
    setv va01 1
  endi

  * Check position if we're in Ainarda. This is done because some of the water
  * rooms overlap with visually dry land areas
  doif va01 eq 0 and gmap posx posy eq va00
    doif posx gt 92818 and posx lt 93763  and post lt 6718
      setv va01 1
    elif posx gt 93958 and posx lt 94356 and post lt 5565
      setv va01 1
    elif posx gt 94470 and posx lt 95476 and post lt 6135
      setv va01 1
    elif posx gt 95454 and posx lt 97605 and post lt 6486
      setv va01 1
    elif posx gt 98460 and post lt 5437
      setv va01 1
    endi
  endi

  * Swim down if close to the surface
  doif va01 eq 1
    velo velx rand 1 3
  endi

  * Change direction at random or if no longer moving
  doif rand 1 3 eq 10 or velx eq 0
    doif ov01 eq 1
      velo 0 0
      anim [22 23 24 25 26]
      over
      anim [11 12 13 14 15 16 17 18 19 20 21 255]
      velo rand 1 2 rand -1 1
      setv ov01 2
    else
      velo 0 0
      anim [26 25 24 23 22]
      over
      anim [0 1 2 3 4 5 6 7 8 9 10 255]
      velo rand -1 -2 rand -1 1
      setv ov01 1
    endi
  endi
endm

rscr

enum 2 15 60708
  kill targ
next
scrx 2 15 60708 9
scrx 2 15 60708 10
